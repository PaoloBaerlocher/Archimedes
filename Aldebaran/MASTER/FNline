REM >Master.FNline

MODE 15:MODE 13:OFF

REM ******** clipped line - September 1991
REM * draw between lines 0 and 207
REM * format : color,X1,Y1,X2,Y2  == { R12,R1,R2,R3,R4}
REM * R0 unaltered
REM *********************************

REM new version , 3.4.1992
 REM faster clip ,and very fast fill for H or V lines

DIM cde 900
FOR PASS=0 TO 2 STEP 2
P%=cde
[OPT PASS
.BASESCR EQUD &1FD8000
FNlineRoutine
]
NEXT

END

DEFFNlineRoutine
[OPT PASS

.PutLine

CMP R1,#320:CMPCC R2,#207:BCS clipLine1
CMP R3,#320:CMPCC R4,#207:BCS clipLine2

LDR R11,BASESCR
ADD R5,R1,R2,LSL#8
ADD R5,R5,R2,LSL#6
ADD R5,R5,R11

SUBS R10,R3,R1 :  BEQ highSpeedLineV
MOVPL R8,#1
MVNMI R8,#0
SUBMI R10,R1,R3
SUBS R2,R4,R2  :  BEQ highSpeedLineH
MOV R9,#320
SUBMI R9,R9,#640
RSBMI R2,R2,#0

SUBS R13,R10,R2
BMI ALTRO

MOV R6,R10
MOV R7,R10,LSR#1
.DOLOOP
SUBS R6,R6,#1
MOVMI PC,R14
STRB R12,[R5],R8
SUBS R7,R7,R2
BPL DOLOOP

STRB R12,[R5,R9]!
SUB R6,R6,#1
ADD R5,R5,R8
ADDS R7,R7,R13
BPL DOLOOP
ADD R7,R7,R10
ADD R5,R5,R9
B DOLOOP


.ALTRO
MOV R6,R2
MOV R7,R2,LSR#1
.DOLOOQ
SUBS R6,R6,#1
MOVMI PC,R14
STRB R12,[R5],R9
SUBS R7,R7,R10
BPL DOLOOQ

STRB R12,[R5,R8]!
SUB R6,R6,#1
ADD R5,R5,R9
SUBS R7,R7,R13
BPL DOLOOQ
ADD R7,R7,R2
ADD R5,R5,R8
B DOLOOQ

.clipLine1
CMP R3,#320:CMPCC R4,#207:MOVCS PC,R14

MOV R5,R1:MOV R1,R3:MOV R3,R5
MOV R5,R2:MOV R2,R4:MOV R4,R5

.clipLine2           ; (R3,R4) outside

LDR R11,BASESCR
ADD R5,R1,R2,LSL#8
ADD R5,R5,R2,LSL#6

SUBS R10,R3,R1    :BEQ highSpeedVclip
MOVPL R8,#1
MVNMI R8,#0
SUBMI R10,R1,R3
MOV R3,R1
SUBS R1,R4,R2     :BEQ highSpeedHclip
MOV R9,#320
SUBMI R9,R9,#640
SUBMI R1,R2,R4

SUBS R13,R10,R1
BMI ALTRO2

MOV R7,R10,LSR#1
.DOLOOP2
STRB R12,[R11,R5]:ADD R3,R3,R8
                  CMP R3,#320:MOVCS PC,R14
                  ADD R5,R5,R8
SUBS R7,R7,R1
BPL DOLOOP2

ADD R5,R5,R9
 CMP R5,#65*1024:MOVCS PC,R14

STRB R12,[R11,R5]
 ADD R3,R3,R8
                 CMP R3,#320:MOVCS PC,R14
ADD R5,R5,R8
ADDS R7,R7,R13
BPL DOLOOP2
ADD R7,R7,R10
ADD R5,R5,R9
 CMP R5,#65*1024:BCC DOLOOP2
MOV PC,R14

.ALTRO2
MOV R7,R1,LSR#1
.DOLOOQ2

STRB R12,[R11,R5]:ADD R5,R5,R9
                   CMP R5,#65*1024:MOVCS PC,R14
SUBS R7,R7,R10
BPL DOLOOQ2

ADD R3,R3,R8:CMP R3,#320:MOVCS PC,R14

ADD R5,R5,R8
STRB R12,[R11,R5]
ADD R5,R5,R9
                  CMP R5,#65*1024:MOVCS PC,R14
SUBS R7,R7,R13
BPL DOLOOQ2
ADD R7,R7,R1
ADD R5,R5,R8
 ADD R3,R3,R8:CMP R3,#320:BCC DOLOOQ2
MOV PC,R14

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

.highSpeedHclip
ADD R5,R5,R11
CMP R8,#1:RSBEQ R10,R3,#320:MOVNE R10,R3   ;reduce line to fit into screen

.highSpeedLineH
RSBS R11,R10,#14:BMI wordFill

ADD PC,PC,R11,LSL#2
EQUS "JUMP"
STRB R12,[R5],R8
STRB R12,[R5],R8
STRB R12,[R5],R8
STRB R12,[R5],R8
STRB R12,[R5],R8
STRB R12,[R5],R8
STRB R12,[R5],R8
STRB R12,[R5],R8
STRB R12,[R5],R8
STRB R12,[R5],R8
STRB R12,[R5],R8
STRB R12,[R5],R8
STRB R12,[R5],R8
STRB R12,[R5]
MOV PC,R14

.wordFill
CMP R8,#1:SUBNE R5,R5,R10

MOVS R7,R5,LSL#30:BEQ noBoundLine

EORS R7,R7,R7,LSL#1
SUB R10,R10,R7,LSR#30

STRCCB R12,[R5],#1
STRMIB R12,[R5],#1
STRB   R12,[R5],#1
.noBoundLine

MOV R13,R12

SUBS R10,R10,#16:BMI dust
.dustloop
STMIA R5!,{R12,R13}
STMIA R5!,{R12,R13}
SUBS R10,R10,#16:BPL dustloop

.dust
TEQP PC,R10,LSL#28
 STMMIIA R5!,{R12,R13}
 STREQ R12,[R5],#4
 STRCSB R12,[R5],#1
 STRCSB R12,[R5],#1
 STRVSB R12,[R5]
MOV PC,R14

.highSpeedVclip
ADD R5,R5,R11
CMP R4,#0:MOVMI R4,#0:MOVPL R4,#208 ;reduce line to fit into screen

.highSpeedLineV
SUBS R2,R4,R2
MOV R9,#320
SUBMI R9,R9,#640
RSBMI R2,R2,#0

.nextV
SUBS R2,R2,#8:BMI littleV
STRB R12,[R5],R9
STRB R12,[R5],R9
STRB R12,[R5],R9
STRB R12,[R5],R9
STRB R12,[R5],R9
STRB R12,[R5],R9
STRB R12,[R5],R9
STRB R12,[R5],R9
BGT nextV

.littleV
SUBNE PC,PC,R2,LSL#2
MOV PC,R14
STRB R12,[R5],R9
STRB R12,[R5],R9
STRB R12,[R5],R9
STRB R12,[R5],R9
STRB R12,[R5],R9
STRB R12,[R5],R9
STRB R12,[R5],R9
STRB R12,[R5]
MOV PC,R14

]
=0
