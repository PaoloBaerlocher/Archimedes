REM >Master.FNsprite
____________________________________________________________________________

DEFFNspriteRoutine
LOCAL g%()
Ymax=208
DIM g%(4):g%()=16,24,36,48,-1

[OPT PASS
                     ; BOOM = 8 phases , 4 profondeurs
.PutExploSprite      ; input R0,R1=(x,y);R2=(phase+8*deep)=spr number

     CMP R2,#%11111:MOVGT R15,R14 ; out of range

LDR R12,[PC,R2,LSL#2]
B skipTableE
]

DELTA%=0:D%=0

WHILE g%(D%)<>-1

FOR PHASE%=0TO 7
[OPT PASS
 EQUD dataExp+DELTA%
]
 DELTA%+=g%(D%)^2
NEXT

D%+=1
ENDWHILE

[OPT PASS

.skipTableE
AND R2,R2,#%11000
LDR R2,[PC,R2,LSR#1]
B PutSprite
EQUD 16/4
EQUD 24/4
EQUD 36/4
EQUD 48/4

;;;;;;;;;;;;;;;;;;;;;;;;;;

.PutSprite        ; R2= taille / 4 du sprite
                  ; (R0,R1) = (x,y)
FNin

MOVS R14,R2,LSL#2:BEQ quit_spr      ; sprite de taille 0x0 ...!?!
MOV R11,R14
SUB R0,R0,R14,LSR#1:SUB R1,R1,R14,LSR#1
                                          ; number of lines to draw
CMP R1,#0:BPL nocutUp
    RSB R1,R1,#0
    MLA R12,R14,R1,R12
    SUB R14,R14,R1:MOV R1,#0              ; cut up

.nocutUp
ADD R13,R1,R14:SUBS R13,R13,#208
    SUBGT R14,R14,R13                     ; cut down

CMP R14,#0:BLE quit_spr                       ; no lines to draw

CMP R0,#320-4:BGT quit_spr                    ; out of RightBorder
ADDS R9,R0,R11:BLE quit_spr                   ; out of LeftBorder

FNldr(13,BASESCR)

BIC R0,R0,#1   ; forget pixelPerfect

TST R0,#2:BNE twoPixelsShift

CMP R0,#0:BMI leftCut
SUBS R9,R9,#320:BPL rightCut

;;;; noCut ;;;;

ADD R13,R13,R0:ADD R13,R13,R1,LSL#8:ADD R13,R13,R1,LSL#6

LDR PC,[PC,R2,LSL#2]
EQUS "JUMP"
EQUD quit_spr
EQUD horiz04
EQUD horiz08
EQUD horiz12
EQUD horiz16
EQUD horiz20
EQUD horiz24
EQUD horiz28
EQUD horiz32
EQUD horiz36
EQUD horiz40
EQUD horiz44
EQUD horiz48

.horiz04
LDR R0,[R13]:LDR R6,[R12],#4:ORR R0,R0,R6:STR R0,[R13],#320
SUBS R14,R14,#1:BNE horiz04
FNexit

.horiz08
LDMIA R13,{R0-R1}:LDMIA R12!,{R6-R7}:ORR R0,R0,R6:ORR R1,R1,R7:STMIA R13!,{R0-R1}
ADD R13,R13,#320-08
SUBS R14,R14,#1:BNE horiz08
FNexit

.horiz12
LDMIA R13,{R0-R2}:LDMIA R12!,{R6-R8}:ORR R0,R0,R6:ORR R1,R1,R7:ORR R2,R2,R8:STMIA R13!,{R0-R2}
ADD R13,R13,#320-12
SUBS R14,R14,#1:BNE horiz12
FNexit

.horiz16
LDMIA R13,{R0-R3}:LDMIA R12!,{R4-R11}:ORR R0,R0,R4:ORR R1,R1,R5:ORR R2,R2,R6:ORR R3,R3,R7:STMIA R13!,{R0-R3}
SUBS R14,R14,#1:BEQ quit_spr
ADD R13,R13,#320-16
LDMIA R13,{R0-R3}:ORR R0,R0,R8:ORR R1,R1,R9:ORR R2,R2,R10:ORR R3,R3,R11:STMIA R13!,{R0-R3}
ADD R13,R13,#320-16
SUBS R14,R14,#1:BNE horiz16
FNexit

.horiz20
LDMIA R13,{R0-R4}:LDMIA R12!,{R5-R11}:ORR R0,R0,R5:ORR R1,R1,R6:ORR R2,R2,R7:ORR R3,R3,R8:ORR R4,R4,R9:STMIA R13,{R0-R4}
SUBS R14,R14,#1:BEQ quit_spr
ADD R13,R13,#320
LDMIA R13,{R0-R4}:ORR R0,R0,R10:ORR R1,R1,R11:LDMIA R12!,{R5-R11}:ORR R2,R2,R5:ORR R3,R3,R6:ORR R4,R4,R7:STMIA R13,{R0-R4}
SUBS R14,R14,#1:BEQ quit_spr
ADD R13,R13,#320
LDMIA R13,{R0-R4}:ORR R0,R0,R8:ORR R1,R1,R9:ORR R2,R2,R10:ORR R3,R3,R11:LDMIA R12!,{R5-R10}:ORR R4,R4,R5:STMIA R13,{R0-R4}
SUBS R14,R14,#1:BEQ quit_spr
ADD R13,R13,#320
LDMIA R13,{R0-R4}:ORR R0,R0,R6:ORR R1,R1,R7:ORR R2,R2,R8:ORR R3,R3,R9:ORR R4,R4,R10:STMIA R13,{R0-R4}
SUBS R14,R14,#1:ADDNE R13,R13,#320:BNE horiz20
FNexit

.horiz24
LDMIA R13,{R0-R5}:LDMIA R12!,{R6-R11}:ORR R0,R0,R6:ORR R1,R1,R7:ORR R2,R2,R8:ORR R3,R3,R9:ORR R4,R4,R10:ORR R5,R5,R11:STMIA R13!,{R0-R5}
ADD R13,R13,#320-24
SUBS R14,R14,#1:BNE horiz24
FNexit

.horiz28
LDMIA R13,{R0-R5}:LDMIA R12!,{R6-R11}:ORR R0,R0,R6:ORR R1,R1,R7:ORR R2,R2,R8:ORR R3,R3,R9:ORR R4,R4,R10:ORR R5,R5,R11
LDR R6,[R13,#24]:LDR R7,[R12],#4:ORR R6,R7,R6:STMIA R13!,{R0-R6}
ADD R13,R13,#320-28
SUBS R14,R14,#1:BNE horiz28
FNexit

.horiz32
LDMIA R13,{R0-R5}:LDMIA R12!,{R6-R11}:ORR R0,R0,R6:ORR R1,R1,R7:ORR R2,R2,R8:ORR R3,R3,R9:ORR R4,R4,R10:ORR R5,R5,R11:STMIA R13!,{R0-R5}
LDMIA R13,{R0-R1}:LDMIA R12!,{R6-R7}:ORR R0,R0,R6:ORR R1,R1,R7:STMIA R13!,{R0-R1}
ADD R13,R13,#320-32
SUBS R14,R14,#1:BNE horiz32
FNexit

.horiz36
LDMIA R13,{R0-R5}:LDMIA R12!,{R6-R11}:ORR R0,R0,R6:ORR R1,R1,R7:ORR R2,R2,R8:ORR R3,R3,R9:ORR R4,R4,R10:ORR R5,R5,R11:STMIA R13!,{R0-R5}
LDMIA R13,{R0-R2}:LDMIA R12!,{R6-R8}:ORR R0,R0,R6:ORR R1,R1,R7:ORR R2,R2,R8:STMIA R13!,{R0-R2}
ADD R13,R13,#320-36
SUBS R14,R14,#1:BNE horiz36
FNexit

.horiz40
LDMIA R13,{R0-R5}:LDMIA R12!,{R6-R11}:ORR R0,R0,R6:ORR R1,R1,R7:ORR R2,R2,R8:ORR R3,R3,R9:ORR R4,R4,R10:ORR R5,R5,R11:STMIA R13!,{R0-R5}
LDMIA R13,{R0-R3}:LDMIA R12!,{R6-R9}:ORR R0,R0,R6:ORR R1,R1,R7:ORR R2,R2,R8:ORR R3,R3,R9:STMIA R13!,{R0-R3}
ADD R13,R13,#320-40
SUBS R14,R14,#1:BNE horiz40
FNexit

.horiz44
LDMIA R13,{R0-R5}:LDMIA R12!,{R6-R11}:ORR R0,R0,R6:ORR R1,R1,R7:ORR R2,R2,R8:ORR R3,R3,R9:ORR R4,R4,R10:ORR R5,R5,R11:STMIA R13!,{R0-R5}
LDMIA R13,{R0-R4}:LDMIA R12!,{R6-R10}:ORR R0,R0,R6:ORR R1,R1,R7:ORR R2,R2,R8:ORR R3,R3,R9:ORR R4,R4,R10:STMIA R13!,{R0-R4}
ADD R13,R13,#320-44
SUBS R14,R14,#1:BNE horiz44
FNexit

.horiz48
LDMIA R13,{R0-R5}:LDMIA R12!,{R6-R11}:ORR R0,R0,R6:ORR R1,R1,R7:ORR R2,R2,R8:ORR R3,R3,R9:ORR R4,R4,R10:ORR R5,R5,R11:STMIA R13!,{R0-R5}
LDMIA R13,{R0-R5}:LDMIA R12!,{R6-R11}:ORR R0,R0,R6:ORR R1,R1,R7:ORR R2,R2,R8:ORR R3,R3,R9:ORR R4,R4,R10:ORR R5,R5,R11:STMIA R13!,{R0-R5}
ADD R13,R13,#320-48

SUBS R14,R14,#1:BNE horiz48
FNexit

;;;; CutLeft ;;;;    ; R11=Xlen sprite
.leftCut

SUB R12,R12,R0 ; move sprite address to the right
ADD R10,R11,R0
RSB R11,R0,#0:BIC R11,R11,#3

ADD R13,R13,R1,LSL#8:ADD R13,R13,R1,LSL#6

.cutSprite
SUB R10,R10,#1:BIC R10,R10,#3
LDR PC,[PC,R10]
EQUD cut04
EQUD cut04
EQUD cut08
EQUD cut12
EQUD cut16
EQUD cut20
EQUD cut24
EQUD cut28
EQUD cut32
EQUD cut36
EQUD cut40
EQUD cut44
EQUD horiz48
EQUD horiz48

;;;; CutRight ;;;;

.rightCut
SUB R10,R11,R9
BIC R11,R9,#3

ADD R13,R13,R0:ADD R13,R13,R1,LSL#8:ADD R13,R13,R1,LSL#6
B cutSprite

.cut04
LDR R0,[R13]:LDR R1,[R12],#4:ADD R12,R12,R11:ORR R0,R0,R1:STR R0,[R13],#320
SUBS R14,R14,#1:BNE cut04
FNexit

.cut08
LDMIA R13,{R0,R1}:LDMIA R12!,{R2,R3}:ADD R12,R12,R11:ORR R0,R0,R2:ORR R1,R1,R3:STMIA R13,{R0,R1}:ADD R13,R13,#320
SUBS R14,R14,#1:BNE cut08
FNexit

.cut12
LDMIA R13,{R0,R1,R2}:LDMIA R12!,{R3,R4,R5}:ADD R12,R12,R11:ORR R0,R0,R3:ORR R1,R1,R4:ORR R2,R2,R5:STMIA R13,{R0,R1,R2}:ADD R13,R13,#320
SUBS R14,R14,#1:BNE cut12
FNexit

.cut16
LDMIA R13,{R0,R1,R2,R3}:LDMIA R12!,{R4,R5,R6,R7}:ADD R12,R12,R11:ORR R0,R0,R4:ORR R1,R1,R5:ORR R2,R2,R6:ORR R3,R3,R7:STMIA R13,{R0,R1,R2,R3}:ADD R13,R13,#320
SUBS R14,R14,#1:BNE cut16
FNexit

.cut20
LDMIA R13,{R0-R4}:LDMIA R12!,{R5-R9}:ADD R12,R12,R11:ORR R0,R0,R5:ORR R1,R1,R6:ORR R2,R2,R7:ORR R3,R3,R8:ORR R4,R4,R9:STMIA R13,{R0-R4}:ADD R13,R13,#320
SUBS R14,R14,#1:BNE cut20
FNexit

.cut24
LDMIA R13,{R0-R4}:LDMIA R12!,{R5-R10}:ADD R12,R12,R11:ORR R0,R0,R5:ORR R1,R1,R6:ORR R2,R2,R7:ORR R3,R3,R8:ORR R4,R4,R9:LDR R5,[R13,#20]:ORR R5,R5,R10:STMIA R13,{R0-R5}:ADD R13,R13,#320
SUBS R14,R14,#1:BNE cut24
FNexit

.cut28
LDMIA R13!,{R0-R3}:LDMIA R12!,{R4-R10}:ADD R12,R12,R11:ORR R0,R0,R4:ORR R1,R1,R5:ORR R2,R2,R6:ORR R3,R3,R7:LDMIA R13!,{R4,R5,R6}:ORR R4,R4,R8:ORR R5,R5,R9:ORR R6,R6,R10:STMDB R13!,{R0-R6}:ADD R13,R13,#320
SUBS R14,R14,#1:BNE cut28
FNexit

.cut32
LDMIA R13,{R0-R3}:LDMIA R12!,{R4-R7}:ORR R0,R0,R4:ORR R1,R1,R5:ORR R2,R2,R6:ORR R3,R3,R7:STMIA R13!,{R0-R3}
LDMIA R13,{R0-R3}:LDMIA R12!,{R4-R7}:ORR R0,R0,R4:ORR R1,R1,R5:ORR R2,R2,R6:ORR R3,R3,R7:STMIA R13!,{R0-R3}:ADD R12,R12,R11
ADD R13,R13,#320-32
SUBS R14,R14,#1:BNE cut32
FNexit

.cut36
LDMIA R13,{R0-R3}:LDMIA R12!,{R4-R7}:ORR R0,R0,R4:ORR R1,R1,R5:ORR R2,R2,R6:ORR R3,R3,R7:STMIA R13!,{R0-R3}
LDMIA R13,{R0-R4}:LDMIA R12!,{R5-R9}:ORR R0,R0,R5:ORR R1,R1,R6:ORR R2,R2,R7:ORR R3,R3,R8:ORR R4,R4,R9:STMIA R13!,{R0-R4}:ADD R12,R12,R11
ADD R13,R13,#320-36
SUBS R14,R14,#1:BNE cut36
FNexit

.cut40
LDMIA R13,{R0-R4}:LDMIA R12!,{R5-R9}:ORR R0,R0,R5:ORR R1,R1,R6:ORR R2,R2,R7:ORR R3,R3,R8:ORR R4,R4,R9:STMIA R13!,{R0-R4}
LDMIA R13,{R0-R4}:LDMIA R12!,{R5-R9}:ORR R0,R0,R5:ORR R1,R1,R6:ORR R2,R2,R7:ORR R3,R3,R8:ORR R4,R4,R9:STMIA R13!,{R0-R4}:ADD R12,R12,R11
ADD R13,R13,#320-40
SUBS R14,R14,#1:BNE cut40
FNexit

.cut44
LDMIA R13,{R0-R4}:LDMIA R12!,{R5-R9}:ORR R0,R0,R5:ORR R1,R1,R6:ORR R2,R2,R7:ORR R3,R3,R8:ORR R4,R4,R9:STMIA R13!,{R0-R4}
LDMIA R13,{R0-R4}:LDMIA R12!,{R5-R10}:ORR R0,R0,R5:ORR R1,R1,R6:ORR R2,R2,R7:ORR R3,R3,R8:ORR R4,R4,R9:LDR R5,[R13,#40]:ORR R5,R5,R10:STMIA R13!,{R0-R5}:ADD R12,R12,R11
ADD R13,R13,#320-44
SUBS R14,R14,#1:BNE cut44
FNexit

.twoPixelsShift
CMP R0,#0:BMI leftCut
SUBS R9,R9,#320:BPL rightCut

;;;; noCut ;;;;

ADD R13,R13,R0:ADD R13,R13,R1,LSL#8:ADD R13,R13,R1,LSL#6

LDR PC,[PC,R2,LSL#2]
EQUS "JUMP"
EQUD quit_spr
EQUD horiz04_
EQUD horiz08_
EQUD horiz12_
EQUD horiz16_
EQUD horiz20_
EQUD horiz24_
EQUD horiz28_
EQUD horiz32_
EQUD horiz36_
EQUD horiz40_
EQUD horiz44_
EQUD horiz48_

.horiz04_
LDMIA R13,{R0-R1}:LDR R6,[R12],#4:ORR R0,R0,R6,LSL#16:ORR R1,R1,R6,LSR#16:STMIA R13!,{R0-R1}
ADD R13,R13,#320-4-4
SUBS R14,R14,#1:BNE horiz04_
FNexit

.horiz08_
LDMIA R13,{R0-R2}:LDMIA R12!,{R6-R7}:ORR R0,R0,R6,LSL#16:ORR R1,R1,R6,LSR#16:ORR R1,R1,R7,LSL#16:ORR R2,R2,R7,LSR#16:STMIA R13!,{R0-R2}
ADD R13,R13,#320-8-4
SUBS R14,R14,#1:BNE horiz08_
FNexit

.horiz12_
LDMIA R13,{R0-R3}:LDMIA R12!,{R6-R8}:ORR R0,R0,R6,LSL#16:ORR R1,R1,R6,LSR#16:ORR R1,R1,R7,LSL#16:ORR R2,R2,R7,LSR#16:ORR R2,R2,R8,LSL#16:ORR R3,R3,R8,LSR#16:STMIA R13!,{R0-R3}
ADD R13,R13,#320-12-4
SUBS R14,R14,#1:BNE horiz12_
FNexit

.horiz16_
LDMIA R13,{R0-R4}:LDMIA R12!,{R6-R9}:ORR R0,R0,R6,LSL#16:ORR R1,R1,R6,LSR#16:ORR R1,R1,R7,LSL#16:ORR R2,R2,R7,LSR#16:ORR R2,R2,R8,LSL#16:ORR R3,R3,R8,LSR#16:ORR R3,R3,R9,LSL#16
ORR R4,R4,R9,LSR#16:STMIA R13!,{R0-R4}
ADD R13,R13,#320-16-4
SUBS R14,R14,#1:BNE horiz16_
FNexit

.horiz20_
LDMIA R13,{R0-R5}:LDMIA R12!,{R6-R10}:ORR R0,R0,R6,LSL#16:ORR R1,R1,R6,LSR#16:ORR R1,R1,R7,LSL#16:ORR R2,R2,R7,LSR#16:ORR R2,R2,R8,LSL#16:ORR R3,R3,R8,LSR#16:ORR R3,R3,R9,LSL#16
ORR R4,R4,R9,LSR#16:ORR R4,R4,R10,LSL#16:ORR R5,R5,R10,LSR#16:STMIA R13!,{R0-R5}
ADD R13,R13,#320-20-4
SUBS R14,R14,#1:BNE horiz20_
FNexit

.horiz24_
LDMIA R13,{R0-R3}:LDMIA R12!,{R6-R11}:ORR R0,R0,R6,LSL#16:ORR R1,R1,R6,LSR#16:ORR R1,R1,R7,LSL#16:ORR R2,R2,R7,LSR#16:ORR R2,R2,R8,LSL#16:ORR R3,R3,R8,LSR#16:ORR R3,R3,R9,LSL#16:STMIA R13!,{R0-R3}
LDMIA R13,{R0-R2}:ORR R0,R0,R9,LSR#16:ORR R0,R0,R10,LSL#16:ORR R1,R1,R10,LSR#16:ORR R1,R1,R11,LSL#16:ORR R2,R2,R11,LSR#16:STMIA R13!,{R0-R2}
ADD R13,R13,#320-24-4
SUBS R14,R14,#1:BNE horiz24_
FNexit

.horiz28_
LDMIA R13,{R0-R3}:LDMIA R12!,{R5-R11}:ORR R0,R0,R5,LSL#16:ORR R1,R1,R5,LSR#16:ORR R1,R1,R6,LSL#16:ORR R2,R2,R6,LSR#16:ORR R2,R2,R7,LSL#16:ORR R3,R3,R7,LSR#16:ORR R3,R3,R8,LSL#16:STMIA R13!,{R0-R3}
LDMIA R13,{R0-R3}:ORR R0,R0,R8,LSR#16:ORR R0,R0,R9,LSL#16:ORR R1,R1,R9,LSR#16:ORR R1,R1,R10,LSL#16:ORR R2,R2,R10,LSR#16:ORR R2,R2,R11,LSL#16:ORR R3,R3,R11,LSR#16:STMIA R13!,{R0-R3}
ADD R13,R13,#320-28-4
SUBS R14,R14,#1:BNE horiz28_
FNexit

.horiz32_
LDMIA R13,{R0-R3}:LDMIA R12!,{R4-R11}:ORR R0,R0,R4,LSL#16:ORR R1,R1,R4,LSR#16:ORR R1,R1,R5,LSL#16:ORR R2,R2,R5,LSR#16:ORR R2,R2,R6,LSL#16:ORR R3,R3,R6,LSR#16:ORR R3,R3,R7,LSL#16:STMIA R13!,{R0-R3}
LDMIA R13,{R0-R4}:ORR R0,R0,R7,LSR#16:ORR R0,R0,R8,LSL#16:ORR R1,R1,R8,LSR#16:ORR R1,R1,R9,LSL#16:ORR R2,R2,R9,LSR#16:ORR R2,R2,R10,LSL#16:ORR R3,R3,R10,LSR#16:ORR R3,R3,R11,LSL#16:ORR R4,R4,R11,LSR#16:STMIA R13!,{R0-R4}
ADD R13,R13,#320-32-4
SUBS R14,R14,#1:BNE horiz32_
FNexit

.horiz36_
LDMIA R13,{R0-R2}:LDMIA R12!,{R3-R11}:ORR R0,R0,R3,LSL#16:ORR R1,R1,R3,LSR#16:ORR R1,R1,R4,LSL#16:ORR R2,R2,R4,LSR#16:ORR R2,R2,R5,LSL#16:STMIA R13!,{R0-R2}
LDMIA R13,{R0-R4}:ORR R0,R0,R5,LSR#16:ORR R0,R0,R6,LSL#16:ORR R1,R1,R6,LSR#16:ORR R1,R1,R7,LSL#16:ORR R2,R2,R7,LSR#16:ORR R2,R2,R8,LSL#16:ORR R3,R3,R8,LSR#16:ORR R3,R3,R9,LSL#16:ORR R4,R4,R9,LSR#16:ORR R4,R4,R10,LSL#16:STMIA R13!,{R0-R4}
LDMIA R13,{R0-R1}:ORR R0,R0,R10,LSR#16:ORR R0,R0,R11,LSL#16:ORR R1,R1,R11,LSR#16:STMIA R13!,{R0-R1}
ADD R13,R13,#320-36-4
SUBS R14,R14,#1:BNE horiz36_
FNexit

.horiz40_
LDMIA R13,{R0-R4}:LDMIA R12!,{R7-R11}:ORR R0,R0,R7,LSL#16:ORR R1,R1,R7,LSR#16:ORR R1,R1,R8,LSL#16:ORR R2,R2,R8,LSR#16:ORR R2,R2,R9,LSL#16:ORR R3,R3,R9,LSR#16:ORR R3,R3,R10,LSL#16:ORR R4,R4,R10,LSR#16:ORR R4,R4,R11,LSL#16:STMIA R13!,{R0-R4}
LDMIA R13,{R0-R5}:LDMIA R12!,{R6-R10}:ORR R0,R0,R11,LSR#16:ORR R0,R0,R6,LSL#16:ORR R1,R1,R6,LSR#16:ORR R1,R1,R7,LSL#16:ORR R2,R2,R7,LSR#16:ORR R2,R2,R8,LSL#16:ORR R3,R3,R8,LSR#16:ORR R3,R3,R9,LSL#16:ORR R4,R4,R9,LSR#16:ORR R4,R4,R10,LSL#16:ORR R5,R5,R10,LSR#16
STMIA R13!,{R0-R5}

ADD R13,R13,#320-40-4
SUBS R14,R14,#1:BNE horiz40_
FNexit

.horiz44_
LDMIA R13,{R0-R5}:LDMIA R12!,{R6-R11}:ORR R0,R0,R6,LSL#16:ORR R1,R1,R6,LSR#16:ORR R1,R1,R7,LSL#16:ORR R2,R2,R7,LSR#16:ORR R2,R2,R8,LSL#16:ORR R3,R3,R8,LSR#16:ORR R3,R3,R9,LSL#16:ORR R4,R4,R9,LSR#16:ORR R4,R4,R10,LSL#16:ORR R5,R5,R10,LSR#16:ORR R5,R5,R11,LSL#16
STMIA R13!,{R0-R5}
LDMIA R13,{R0-R5}:LDMIA R12!,{R6-R10}:ORR R0,R0,R11,LSR#16:ORR R0,R0,R6,LSL#16:ORR R1,R1,R6,LSR#16:ORR R1,R1,R7,LSL#16:ORR R2,R2,R7,LSR#16:ORR R2,R2,R8,LSL#16:ORR R3,R3,R8,LSR#16:ORR R3,R3,R9,LSL#16:ORR R4,R4,R9,LSR#16:ORR R4,R4,R10,LSL#16:ORR R5,R5,R10,LSR#16
STMIA R13!,{R0-R5}

ADD R13,R13,#320-44-4

SUBS R14,R14,#1:BNE horiz44_
FNexit

.horiz48_
LDMIA R13,{R0-R5}:LDMIA R12!,{R6-R11}:ORR R0,R0,R6,LSL#16:ORR R1,R1,R6,LSR#16:ORR R1,R1,R7,LSL#16:ORR R2,R2,R7,LSR#16:ORR R2,R2,R8,LSL#16:ORR R3,R3,R8,LSR#16:ORR R3,R3,R9,LSL#16:ORR R4,R4,R9,LSR#16:ORR R4,R4,R10,LSL#16:ORR R5,R5,R10,LSR#16:ORR R5,R5,R11,LSL#16
STMIA R13!,{R0-R5}
LDMIA R13,{R0-R5}:ORR R0,R0,R11,LSR#16:LDMIA R12!,{R6-R11}
ORR R0,R0,R6,LSL#16:ORR R1,R1,R6,LSR#16:ORR R1,R1,R7,LSL#16:ORR R2,R2,R7,LSR#16:ORR R2,R2,R8,LSL#16:ORR R3,R3,R8,LSR#16:ORR R3,R3,R9,LSL#16:ORR R4,R4,R9,LSR#16:ORR R4,R4,R10,LSL#16:ORR R5,R5,R10,LSR#16:ORR R5,R5,R11,LSL#16
STMIA R13!,{R0-R5}

ADD R13,R13,#320-48

SUBS R14,R14,#1:BNE horiz48_

.quit_spr FNout

FNbin

]
=0
